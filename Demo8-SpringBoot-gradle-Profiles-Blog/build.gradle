description = 'Gradle Profiles Blog'

// =================================================================

buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        maven { url 'http://repo.spring.io/plugins-release' }
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${spring_boot_version}")
        classpath("io.spring.gradle:dependency-management-plugin:0.6.0.RELEASE")
    }
}

// =================================================================

allprojects {
    group = 'org.jdw'
    version = '0.0.1-SNAPSHOT'

    apply plugin: 'eclipse'
    apply plugin: 'idea'

	// customize The Wrapper
    task wrapper(type: Wrapper) {
        gradleVersion = '2.14.1'
    }
}

// =================================================================

subprojects {
    // This plugin allows us to execute any JAR file in the project by specifying the main entry class for execution as above.
    // To execute : gradle run -q // -q supress the output build
    // can use : mainClassName = ‘com.petehouston.greet.Program’ in place of manifest ...
    apply plugin: 'application'

    apply plugin: 'java'
    apply plugin: 'war'
    apply plugin: 'spring-boot'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    apply from: "$rootProject.projectDir/gradle/eclipse.gradle"

    repositories {
        mavenLocal()
        mavenCentral()
    }
    
    dependencies {
        
        // Start: JUnit
        testCompile('org.springframework.boot:spring-boot-starter-test') {
            exclude module: 'commons-logging'
        }

        // Start: Spring Boot Web
        compile('org.springframework.boot:spring-boot-starter-web') {
            exclude module: 'spring-boot-starter-tomcat'
        }

        // Start: Database
        compile 'org.springframework.boot:spring-boot-starter-data-jpa'
        compile 'org.hibernate:hibernate-validator:5.1.3.Final' //JSR303 Validation Support
        
        compile 'org.liquibase:liquibase-core'
        compile 'com.mattbertolini:liquibase-slf4j:1.2.1' 

        testCompile 'com.h2database:h2'

        // Start: Apache Commons
        compile 'org.apache.commons:commons-lang3:3.4'

        // Start: Spring Boot Actuator
        compile 'org.springframework.boot:spring-boot-starter-actuator'
    }

    // task To run a project in place without building a jar first
    bootRun {
        // Making static classpath resources reloadable | Ensures IntelliJ can load resource files
        addResources = false
    }
}

// =================================================================

// ADD external gradle file  
apply from: "$rootProject.projectDir/gradle/profiles.gradle"
